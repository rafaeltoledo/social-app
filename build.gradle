buildscript {

    ext.versions = [

        'kotlin'  : '1.3.72',
        'jacoco'  : '0.8.5',
        'koin'    : '2.1.6',
        'mockk'   : '1.10.0',
        'detekt'  : '1.10.0',

        'androidx': [
            'archcore'  : '2.1.0',
            'espresso'  : '3.2.0',
            'junitext'  : '1.1.1',
            'lifecycle' : '2.2.0',
            'testcore'  : '1.2.0',
        ],
    ]

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.google.gms:google-services:4.3.3'
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$versions.detekt"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if (details.requested.group == 'org.jetbrains.kotlin') {
                    details.useVersion versions.kotlin
                }

                if (details.requested.group == 'org.jacoco') {
                    details.useVersion versions.jacoco
                }

                if (details.requested.group == 'androidx.arch.core') {
                    details.useVersion versions.androidx.archcore
                }

                if (details.requested.group == 'androidx.lifecycle') {
                    details.useVersion versions.androidx.lifecycle
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

buildscript {

    ext.versions = [

        'kotlin'  : '1.3.60',
        'jacoco'  : '0.8.5',
        'koin'    : '2.0.1',
        'mockk'   : '1.9.2',
        'detekt'  : '1.1.1',

        'androidx': [
            'annotation': '1.1.0',
            'archcore'  : '2.1.0',
            'espresso'  : '3.2.0',
            'junitext'  : '1.1.1',
            'lifecycle' : '2.1.0',
            'testcore'  : '1.2.0',
        ],
    ]

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.google.gms:google-services:4.3.3'
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$versions.detekt"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if (details.requested.group == 'org.jetbrains.kotlin') {
                    details.useVersion versions.kotlin
                }

                if (details.requested.group == 'org.jacoco') {
                    details.useVersion versions.jacoco
                }

                if (details.requested.group == 'androidx.annotation') {
                    details.useVersion versions.androidx.annotation
                }

                if (details.requested.group == 'androidx.arch.core') {
                    details.useVersion versions.androidx.archcore
                }

                if (details.requested.group == 'androidx.lifecycle') {
                    details.useVersion versions.androidx.lifecycle
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

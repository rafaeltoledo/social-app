plugins {
    id 'com.github.kt3k.coveralls' version '2.12.0'
}

apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.kapt'

apply from: "$rootDir/gradle/coverage.gradle"
apply from: "$rootDir/gradle/lint.gradle"

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId 'net.rafaeltoledo.social'
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName '1.0.0'

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner 'net.rafaeltoledo.social.SocialAppTestRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures.dataBinding true

    sourceSets {
        final sharedTestDir = 'src/sharedTest/kotlin'

        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += ['src/test/kotlin', sharedTestDir]
        androidTest.java.srcDirs += ['src/androidTest/kotlin', sharedTestDir]
    }

    signingConfigs {
        debug {
            storeFile file("$rootDir/distribution/debug.keystore")
        }

        release {
            storeFile file("$rootDir/distribution/release.keystore")
            storePassword System.env.RELEASE_STORE_PASSWORD
            keyAlias System.env.RELEASE_KEY_ALIAS
            keyPassword System.env.RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            buildConfigField 'String', 'GOOGLE_REQUEST_ID_TOKEN', "\"$System.env.GOOGLE_REQUEST_ID_TOKEN_DEVELOPMENT\""

            resValue 'string', 'fb_app_id', "\"$System.env.FB_APP_ID_DEVELOPMENT\""
            resValue 'string', 'fb_login_protocol_scheme', "\"$System.env.FB_LOGIN_PROTOCOL_SCHEME_DEVELOPMENT\""

            multiDexEnabled true

            applicationIdSuffix '.dev'
            signingConfig signingConfigs.debug
        }

        release {
            buildConfigField 'String', 'GOOGLE_REQUEST_ID_TOKEN', "\"$System.env.GOOGLE_REQUEST_ID_TOKEN_RELEASE\""

            resValue 'string', 'fb_app_id', "\"$System.env.FB_APP_ID_RELEASE\""
            resValue 'string', 'fb_login_protocol_scheme', "\"$System.env.FB_LOGIN_PROTOCOL_SCHEME_RELEASE\""

            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests.includeAndroidResources true
    }
}

def bothTestImplementation(String dependency) {
    project.dependencies.add('testImplementation', dependency)
    project.dependencies.add('androidTestImplementation', dependency)
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.androidx.lifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.androidx.lifecycle"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3'

    implementation 'com.google.firebase:firebase-core:18.0.3'
    implementation 'com.google.firebase:firebase-auth:20.0.4'

    implementation 'com.google.android.gms:play-services-auth:19.0.0'
    implementation 'com.facebook.android:facebook-login:9.1.1'

    implementation "org.koin:koin-android:$versions.koin"
    implementation "org.koin:koin-androidx-viewmodel:$versions.koin"

    bothTestImplementation "androidx.test:core-ktx:$versions.androidx.testcore"
    bothTestImplementation "androidx.test.ext:junit-ktx:$versions.androidx.junitext"
    bothTestImplementation "androidx.test.ext:truth:$versions.androidx.testcore"
    bothTestImplementation "androidx.test:runner:$versions.androidx.testcore"
    bothTestImplementation "androidx.test:rules:$versions.androidx.testcore"
    bothTestImplementation "androidx.test.espresso:espresso-core:$versions.androidx.espresso"
    bothTestImplementation "androidx.test.espresso:espresso-intents:$versions.androidx.espresso"
    bothTestImplementation 'com.google.truth:truth:1.1.2'
    bothTestImplementation "org.koin:koin-test:$versions.koin"
    bothTestImplementation "androidx.arch.core:core-testing:$versions.androidx.archcore"

    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation "io.mockk:mockk:$versions.mockk"

    androidTestImplementation 'com.github.tmurakami:dexopener:2.0.5'
    androidTestImplementation "io.mockk:mockk-android:$versions.mockk"

    androidTestUtil "androidx.test:orchestrator:$versions.androidx.testcore"
}

apply plugin: 'com.google.gms.google-services'

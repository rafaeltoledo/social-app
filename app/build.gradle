apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.kapt'

apply from: "$rootDir/gradle/coverage.gradle"

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId 'net.rafaeltoledo.social'
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName '1.0'

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner 'net.rafaeltoledo.social.SocialAppTestRunner'
    }

    dataBinding.enabled true

    sourceSets {
        final sharedTestDir = 'src/sharedTest/kotlin'

        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += ['src/test/kotlin', sharedTestDir]
        androidTest.java.srcDirs += ['src/androidTest/kotlin', sharedTestDir]
    }

    signingConfigs {
        debug {
            storeFile file("$rootDir/distribution/debug.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile file("$rootDir/distribution/release.keystore")
            storePassword System.env.RELEASE_STORE_PASSWORD
            keyAlias System.env.RELEASE_KEY_ALIAS
            keyPassword System.env.RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            buildConfigField 'String', 'GOOGLE_REQUEST_ID_TOKEN', "\"$System.env.GOOGLE_REQUEST_ID_TOKEN_DEVELOPMENT\""

            testCoverageEnabled true

            applicationIdSuffix '.dev'
            signingConfig signingConfigs.debug
        }

        release {
            buildConfigField 'String', 'GOOGLE_REQUEST_ID_TOKEN', "\"$System.env.GOOGLE_REQUEST_ID_TOKEN_RELEASE\""

            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests.includeAndroidResources true
    }
}

def bothTestImplementation(String dependency) {
    project.dependencies.add("testImplementation", dependency)
    project.dependencies.add("androidTestImplementation", dependency)
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle"
    kapt "androidx.lifecycle:lifecycle-compiler:$versions.lifecycle"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.1'

    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.google.firebase:firebase-auth:16.1.0'

    implementation 'com.google.android.gms:play-services-auth:16.0.1'

    implementation "org.koin:koin-android:$versions.koin"
    implementation "org.koin:koin-androidx-viewmodel:$versions.koin"

    bothTestImplementation "androidx.test:core:$versions.testcore"
    bothTestImplementation "androidx.test.ext:junit:$versions.testcore"
    bothTestImplementation "androidx.test.ext:truth:$versions.testcore"
    bothTestImplementation "androidx.test:runner:$versions.astl"
    bothTestImplementation "androidx.test:rules:$versions.astl"
    bothTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
    bothTestImplementation "androidx.test.espresso:espresso-intents:$versions.espresso"
    bothTestImplementation 'com.google.truth:truth:0.42'
    bothTestImplementation "org.koin:koin-test:$versions.koin"
    bothTestImplementation 'androidx.arch.core:core-testing:2.0.0'

    testImplementation 'org.robolectric:robolectric:4.1'
    testImplementation "io.mockk:mockk:$versions.mockk"

    androidTestImplementation "io.mockk:mockk-android:$versions.mockk"

    androidTestUtil "androidx.test:orchestrator:$versions.astl"
}

apply plugin: 'com.google.gms.google-services'

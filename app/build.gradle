plugins {
    id 'com.github.kt3k.coveralls' version '2.12.2'
}

apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'com.google.gms.google-services'

apply from: "$rootDir/gradle/coverage.gradle"
apply from: "$rootDir/gradle/lint.gradle"

android {
    compileSdk 33
    namespace 'net.rafaeltoledo.social'

    defaultConfig {
        applicationId 'net.rafaeltoledo.social'
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName '1.0.0'

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner 'net.rafaeltoledo.social.SocialAppTestRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
        buildConfig true
    }

    sourceSets {
        final sharedTestDir = 'src/sharedTest/kotlin'

        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += ['src/test/kotlin', sharedTestDir]
        androidTest.java.srcDirs += ['src/androidTest/kotlin', sharedTestDir]
    }

    signingConfigs {
        debug {
            storeFile file("$rootDir/distribution/debug.keystore")
        }

        release {
            storeFile file("$rootDir/distribution/release.keystore")
            storePassword System.env.RELEASE_STORE_PASSWORD
            keyAlias System.env.RELEASE_KEY_ALIAS
            keyPassword System.env.RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            buildConfigField 'String', 'GOOGLE_REQUEST_ID_TOKEN', "\"$System.env.GOOGLE_REQUEST_ID_TOKEN_DEVELOPMENT\""

                resValue 'string', 'fb_app_id', "\"$System.env.FB_APP_ID_DEVELOPMENT\""
            resValue 'string', 'fb_login_protocol_scheme', "\"$System.env.FB_LOGIN_PROTOCOL_SCHEME_DEVELOPMENT\""

            multiDexEnabled true

            applicationIdSuffix '.dev'
            signingConfig signingConfigs.debug
        }

        release {
            buildConfigField 'String', 'GOOGLE_REQUEST_ID_TOKEN', "\"$System.env.GOOGLE_REQUEST_ID_TOKEN_RELEASE\""

            resValue 'string', 'fb_app_id', "\"$System.env.FB_APP_ID_RELEASE\""
            resValue 'string', 'fb_login_protocol_scheme', "\"$System.env.FB_LOGIN_PROTOCOL_SCHEME_RELEASE\""

            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packaging {
        resources.excludes.add('META-INF/*')
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests.includeAndroidResources true
    }
}

def bothTestImplementation(String dependency) {
    project.dependencies.add('testImplementation', dependency)
    project.dependencies.add('androidTestImplementation', dependency)
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.androidx.lifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.androidx.lifecycle"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

    implementation 'com.google.firebase:firebase-core:21.1.1'
    implementation 'com.google.firebase:firebase-auth-ktx:22.1.0'

    implementation 'com.google.android.gms:play-services-auth:20.6.0'
    implementation 'com.facebook.android:facebook-login:16.1.3'

    implementation "io.insert-koin:koin-android:$versions.koin"

    bothTestImplementation 'androidx.test:core-ktx:1.5.0'
    bothTestImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    bothTestImplementation 'androidx.test.ext:truth:1.5.0'
    bothTestImplementation 'androidx.test:runner:1.5.2'
    bothTestImplementation 'androidx.test:rules:1.5.0'
    bothTestImplementation "androidx.test.espresso:espresso-core:$versions.androidx.espresso"
    bothTestImplementation "androidx.test.espresso:espresso-intents:$versions.androidx.espresso"
    bothTestImplementation 'com.google.truth:truth:1.1.3'
    bothTestImplementation "io.insert-koin:koin-test:$versions.koin"
    bothTestImplementation "androidx.arch.core:core-testing:$versions.androidx.archcore"

    testImplementation 'org.robolectric:robolectric:4.10.3'
    testImplementation "io.mockk:mockk:$versions.mockk"

    androidTestImplementation 'com.github.tmurakami:dexopener:2.0.5'
    androidTestImplementation "io.mockk:mockk-android:$versions.mockk"

    androidTestUtil 'androidx.test:orchestrator:1.4.2'
}
